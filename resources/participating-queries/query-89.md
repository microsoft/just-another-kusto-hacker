[Click to run](https://dataexplorer.azure.com/clusters/help/databases/Samples?query=H4sIAAAAAAAEAO1d65PaRhL/vn/F2JW6A4f1SiNgwTk+OGvn4nvEOdt3latUaktIAygrNFiPXUhy//t194yeSCxgJ8a2qF0B8+hu9a+nZxpNSxcX7K8iEKHts5kv784uLuCPsdcijliyYgs7dB3pCpfdiE3EOnwwPJ96cbfHAhk4gnXG6juzA5e5dmyzzrB/Pt3Egi3t9WMWJauVDGPhdh8TXWZ22YvAiz3b934REYsXgijHNvSY2hEwkgGVOjKA0iDuYX0PviZBLELiQ6w1Pd5lV7bvJD4QKJALhb3M6fVZCN1dEF99iLDQkX6yDKIeKxa6nj2Xge13FXWry57OgO0dqCFi0cr34iqTjsVRA8wD8eYoYBjamy5+lcxmpInYnvqC3XnxAopdsWa+lDfJSrHod9nXxfOmFj0GFOdCM3NkGIpoJQPXC+bIms1kSDWk8ZWMQKHQHXXzw8tXUValOAy67BsPzsnfoFJvRajJgoD/DjxEl3qugIcIdJ1M4lUSsy9BHHh/nJrFM3Hr2cgrYrNQLqkpouSCfp5Qi3N2tbDhr89cCYAEMmZyNvMcj/iLtRcBonbMll7gLZMlG6XKv5OJ77KpYKF4m3ihcB+z5+tYqHNGProdSD1inchbroDeFE9ptWErO4qxHZ6Hs7CDOX4J7KWIuqB33weiUeIjSOzpsx+YE9rRIqX7NhGhJ6LHWvyXAdBNbVjOlIq9KLdkEJ90pOAKxDomSKa+dG5YtEhPY06jKsbzeLPw0vPTJ8ciucRDiNqBQicJI+9WsFkSOAQlnCXQEKQ5AIUAQAEfsf8IaAot0JwAJTj/5ZJge3SmUXoRRCsvJJgyQ3HCzSqW89BeLTwH7G8Ox84VQy0K7E6tu0/YIo5X0ZOLizlQT6aPgfjF0g4d75dFcqGA5YayKni9KPVmjh2g0GBg3swDg04iUHFOUga+F4i5q6j+d/H8Zv79D98ZmdBob2S0vlAf2IS5GwDRczo/mibvMXNswGilf/xmWfDJgq+jMfz3ocSED+MhlA6g9BL++yNsR1VwGA+gBHoOsfSyrzpxE74YeOhD9WiA30Y/db8iMQDY1+CnQBKLqxLyPbrM1GXkm+C7sTazbkXhjfUUZDHWY47HwSUeDQePMyqxRnic9vFoUvmQ2tjUy7HweGlS7ZAo0GeD2gzGVE50xkRBUK8RHQeKJlGwqD0XOR0+pfZEwaCWNs/pC/rsDFNdKIdfOi2TmHz9lDo9r7IqlhtE0qLaPn1GTYPIs5T81bdP4Y8b11cvv3v95ul3b4BVLCPH9u2wkyg/dY2HlQTfGl2j/7kGHww21nko1isc+uiKcdjePOxqqivbuekDpY79RMvdPfv1DI03RjKdKTjGcHMtw/QT+IVZfO2LWdxRLewfjZ9guusYMPZGXfi0TxeTupiHdOHUhVe61DS0qKGlGuLr7H9ag3p8XoNrRXNcgvcJ1UTboVNegbJitp4Y9O038MboXxlNvoZB+kYWmdZJeR2a0K4j33NEZwskwBAEAdtRrXwRzOPFdjMlZw1X80iu5jtx5Udy5e/E1TqSq/VOXPv7cKUlVh2SUNFIebA35Tq0dlEe7k25DpFdlC/3plyn9V2UR3tT7h9Iebw35cFhlM29xjxRHh5Iea9xTZQvD6Scj12admvb7DXSaD472OrNvcaTpn2g3Zt7jShNew/LX4XyZ+HE5/advWHrcqHiCPyQwTVR6TxKOy5vz/WMiqvMay8WSwpLJp6WtDpnv3r55h84y66f+DKYQ3BI71129isjkrsmQAh2WFCd8UJvvqAqCK9gUR4Up7mU6b9eIcs3T3By9zBQnKo3R725+NZFGUiEN2WF22vobEMffId1kIPvsPZy8d2ltrAq/da+pTgJQgoXQjha8CfLKSzBMQiIvHkA61wMFmDCxaW2o0NRWA6nYcIGIMKoDj/LcMOSQHfTceMdLMgX7O9JBGyykEkHG2wj4hqxtaZAJrATVHQHir+EU+nikuob/apYmFvpmSKxBlBcULS97h7UvYQ9UTCHOyk4VQoOCu3uFHq6Q+jpGlHbyXKre1lopGDynRSq+j5K03WqPozAtrJHu0mcprIv6wmkLgkjPhuaTZEQMsQhCe8uDsl1Ov4dnLUwPqSfVTqeGufsRwz3QC8I6U+qZWg6Bi6Eocdv7Gfwq+wG3NjECyAyr6yWIXz9Ar3RY/Bwkwn7gvwPfilL581mHdUCuJEnhPPpKonzun6pzinVjUp1bqkOo7isEjhHyRKCb4w17YmeFuwbAcrdeLMO+C6bPQBBKFrYrp+iVGm9s13voGRpvbtd76J0qj7XZiWqgLBM/xIRySR0xOSVvCv65w4h0C0qsUfOFmj3GJ40BNuavGPWwwrBIkSKY4zjc1hNgtV8/7CaO2Ad7IB1vAtW68RhNY+A1bwHVjOFldfDij/D4E8t+LtLjisnXPn7x5XvwHW4A1fT2AVs/8SB5UcAy+8BlqfAWvXAgrFf4u9t8D/IgbUIWOv9A2vtAPZyF7DmLmAHJw6sdQSw1j3AWhl5l9yrookevEdOgo68p9g3+k9obNdhgfqe1gLBSYx6t+meKADuES7Tvc9lGjl5fjwAZhMAvAkAqwkA44QBOMK1ufe5NjMnbx0PAG8CwGoCwGgCwDxhAI5wQe59Lojn5PvHA2A1AWA0AWA2AcBPGID+EQD0950DuIqcUgBcUr1LqnetnmJ/+BzQOAIa54DTHQH8iGiI7x0NQVPzeAAa54DGEdA4B5zuCOBHxC1877gFmvLjAWicAxpXQY1zwOmugvgR8QXfO76AptbxADTOAY2roMY54HRXQfyIOIDvHwfwUhzAaRLmNAlzmoT5Zx8H8CPiAL5/HMBLccCBAHwWcQA/Ig7g+8cBvBQHHAjAZxEH8CPiAL5/HMBLccCBAHwWcQA/Ig7ge8cBuP9NbXOd0LWaFAiajDlNxpwmYyBZvhokjab5QA6a0JDmdicnrdruRYhUuJpNTlAOm6YhaW53yrjy+oFY4cqbRr68bPK9ctQ0+0navlBjfBWmVpO1y36Tv5HjJo8vze1ORaa51UtjUjBICTgA1GSSSKVUZ2JJVsdLdRxLsjqrVGdhia47Y4WX7Jfa9elcUxqDUt2ADC2tG5bqhmQOad1lqe6SQKvjPSq1GxGEKY1xqW5Mms50UlYYWi4aelZd0ZmpLLJOArOsQjQMtNCMUlmLCCHaUlZdVh5evEDUs+qy/vAncBx0VF23dUTtGWGlDSRkDXgCKBmyRx5ICDWOmkWtjen86SxJfpKSZCGO9+9BYVfeaiHCr3HD9aQoSyxZvFkJOevQ7hNNaN8LDqXrDeWxVmDISte4ixVfZpcMmq6Qf72JhVEmULPppUCzx6xHI33JHN6wv3lYf17tzw/rb1b7W4f1N/L+FSOCqQXplQ2IOGhB9fmqN2PbLDSBLQeVE16iPfteFHd02b22hbuHX1CKRkqlYlNfZbvT03mRFbbW0vb1vVlga+Qxoc3u9cZ7B6oULO/zlwkb9iuaTOn08nZKTJXE8UxtpadOueANgyOf8GFgZOTqRoMmu20LtMslEynVfN2wKCyltC/JEcuZVAbRUkSRPRdqVVjdFB7LdEu4SsPpsTde7GPjhy9JGQ+VZiirpagYhR91mhRQfEeelNQALNWyKYejlwtwlm/TC4UMXcCAuvdSpnqL35l6PZhMPsifZr+VgVJJP/k4ck8uw28v/xZFt1abe9Lmnvw+uScXhbRKndMVaWu7kkvwlAKGRxKycvJgpNIGJdSqCuzyOaWxoH85f5Qt0YquY7OlyDyPVCdO0lcaANUEUKzPiWaZpVg8925FQJ0QxbyETC7Vf5ta06bWbHNtU2va1Jo2taZNrXnfqTU62R4WCrAu1Xc10AnscgZTanznRYLhbHoeSlwA60z2W9tPBFtjVnyQ3RUgyuawPzpZR53G9yKEQGDJbPY2sUOYNs/VvRj0rKwmW5J8W9I2w6fN8GkzfD4mZb9zhk/ZaRTu+6H9BS7U8c4qzsITtyK72Uh2Z5eevq8L+1P5Xi7PJHrJpQwzgnRfELoTSHYbkE06pMHdLmVMN6EJhLojhxfh+He8Kd5GxZcYWHTgtKY2FrgJOZEl9ArpLitRslzRzxr6hF6RzzMv0D9eKQEN+Gi1GU1tRlOb0dRmNLUZTW1GU5vR9DtkNFXm3md6TQDx4znrt/lObb7TR7HPsc13+uAAtPlOn2i+UzZD8ProrM2FanOhTn10tLlQHxyANhfqE82FqswPdRFEmynVZkqd+vhoM6U+NABtptQnmimld619j3vA8CrT1o6/9AEF2RUstT8tvd5dzbKizYMxNabHGqj9gXpXrZypTbW0P3q/pKwCvVD44tYO4vyhASV66TV6ukTWJnR9sgldYBGFsdLFi5tvE8+5Ebhj2w5w6EKpHeFjKHDLQvDnmB5agU9usPFxCvIuajPDUt6fe2YYujhwXzE+R0SncnjpQ1zCEP2mSmzAXU968xNui8H0AejgFTJMPsYMMzj7p65b3YIcZVk0uM1bpX6cSkIaSPx8HYe2o+YE3LZeeJ5LeUt6m8FWyWDDcFCcK4PeVlet4b+PzDfCLB069tz21ON+cu8LxWCdcz3Pq+cMZZO8lmqWgO9W6UTVIXB0Nl0u2LY26Gk9Eh+os8BtdsrQdmjoXfLy0uQKesCRyuHRY5ByKrRg9JSebKPhH5XHB4LRY4X0pj9cI+onDEX4mKwH7DUoY/Mgbfs6b0YblsqPDgKtpXsQ9b5P0Mm7pgvWLVT1M6DorKYbfLJUypCy15QDJyjTbm4x+SJV8vvNQgQpQZDMb/0umYnA459g7PlsFkl2JyjHTD9Aq/DsrA+ay6hge+qDgKlkCpvCs73gY7SAwIYeCgYLQrCQQmbLH5wJqbf3EY+pBPspDFQalu8hVfL/sBg/T9NuAAA=)

```kql
// General flow
//
//  Sets up hardcoded keys (256-bit), nonce (96-bit) and data (64-byte max. supported).
//   1) Initializes the keystate based on the constant, key, counter and nonce.
//   2) Calculates the keystream based on 4 round (2 rounds on columns, 2 rounds on diagonal)
//   3) Afterwards splits the keystream (32-bit integer array) into a byte table with index lookup
//   4) Based on the index, it gets the corresponding key for the data position and XORs the data
//   5) Finally converts the to Unicode and presents the output + input.
//
//  Deviations from the standard:
//  - ChaCha4 does not officially exist, at minimum 8 rounds would be required. Extending the rounds to 8 (simply by copy pasting and changing names) will result in ADX crashing the queries.
//  - Only 64-byte of data is supported, as then the next key block should be generated. This would require some sort of recursive function to be efficient.
//

// * Version without comments *

//
// Inspiration for the cryptographic logic (C implementation): https://github.com/marcizhu/ChaCha20
//      Implementation can be verified using: https://onlinegdb.com/YhEkgPXN0
//
// Input data
let data = dynamic([112, 190, 20, 202, 133, 230, 89, 84, 119, 96, 250, 70, 48, 134, 114, 95, 42, 68, 174, 84, 21, 101, 145, 85, 108]);
let keySize = 32;
let nonceSize = 12;
let count = 0x1;
let key = dynamic([0xb0, 0x92, 0x57, 0x0c, 0xf2, 0x38, 0xb4, 0x1c, 0x67, 0xa0, 0xc3, 0x71, 0x16, 0x51, 0x07, 0x59, 0x12, 0x94, 0xe0, 0x80, 0x54, 0x13, 0x37, 0x2e, 0x16, 0x2b, 0xe9, 0x04, 0xa2, 0x51, 0xe2, 0xc6]);
let nonce = dynamic([0x10, 0xBA, 0x5E, 0x13, 0x37, 0xBA, 0x5E, 0x01, 0x33, 0x41, 0x85, 0x9f]);
let CHACHA20_CONSTANT = toscalar(unicode_codepoints_from_string("expand 32-byte k"));
let pack4 = (a:dynamic)
{
    toint(binary_or(binary_shift_left(toint(a[0]), (0 * 8)), binary_or(binary_shift_left(toint(a[1]), (1 * 8)), binary_or(binary_shift_left(toint(a[2]), (2 * 8)), binary_shift_left(toint(a[3]), (3 * 8))))))
};
let ChaCha20_init = materialize(
    print x=0
    | extend state00 = toint(toscalar(pack4(array_slice(CHACHA20_CONSTANT, 0 * 4, array_length(CHACHA20_CONSTANT)))))
    | extend state01 = toint(toscalar(pack4(array_slice(CHACHA20_CONSTANT, 1 * 4, array_length(CHACHA20_CONSTANT)))))
    | extend state02 = toint(toscalar(pack4(array_slice(CHACHA20_CONSTANT, 2 * 4, array_length(CHACHA20_CONSTANT)))))
    | extend state03 = toint(toscalar(pack4(array_slice(CHACHA20_CONSTANT, 3 * 4, array_length(CHACHA20_CONSTANT)))))
    | extend state04 = toint(toscalar(pack4(array_slice(key, 0 * 4, array_length(key)))))
    | extend state05 = toint(toscalar(pack4(array_slice(key, 1 * 4, array_length(key)))))
    | extend state06 = toint(toscalar(pack4(array_slice(key, 2 * 4, array_length(key)))))
    | extend state07 = toint(toscalar(pack4(array_slice(key, 3 * 4, array_length(key)))))
    | extend state08 = toint(toscalar(pack4(array_slice(key, 4 * 4, array_length(key)))))
    | extend state09 = toint(toscalar(pack4(array_slice(key, 5 * 4, array_length(key)))))
    | extend state10 = toint(toscalar(pack4(array_slice(key, 6 * 4, array_length(key)))))
    | extend state11 = toint(toscalar(pack4(array_slice(key, 7 * 4, array_length(key)))))
    | extend state12 = toint(count)
    | extend state13 = toint(toscalar(pack4(array_slice(nonce, 0 * 4, array_length(key)))))
    | extend state14 = toint(toscalar(pack4(array_slice(nonce, 1 * 4, array_length(key)))))
    | extend state15 = toint(toscalar(pack4(array_slice(nonce, 2 * 4, array_length(key)))))
    | project-away x
    | project state = pack_array(*)
    | mv-expand with_itemindex=id state);
let CHACHA20_ROTL = (x:long, n:long) 
{ 
    binary_or(binary_shift_left(x,  n), binary_shift_right(x, (32 - n)))
};
let CHACHA20_QR = (T: (a:int, b:int, c:int, d:int)) 
{
    T
    | extend ax = a, bx = b, cx = c, dx = d
    // Have to handle the numbers as signed 64-bit for calculation, as they are in theory unsigned 32-bit which Kusto does not support yet
    | extend ax = binary_and(tolong(ax + bx), 0xFFFFFFFF)
    | extend dx = binary_and((binary_xor(dx, ax)), 0xFFFFFFFF)
    | extend dx = binary_and((CHACHA20_ROTL(dx, 16)), 0xFFFFFFFF)
    | extend cx = binary_and((cx + dx), 0xFFFFFFFF)
    | extend bx = binary_and((binary_xor(bx, cx)), 0xFFFFFFFF)
    | extend bx = binary_and((CHACHA20_ROTL(bx, 12)), 0xFFFFFFFF)
    | extend ax = binary_and((ax + bx), 0xFFFFFFFF)
    | extend dx = binary_and(((binary_xor(dx, ax))), 0xFFFFFFFF)
    | extend dx = binary_and(((CHACHA20_ROTL(dx, 8))), 0xFFFFFFFF)
    | extend cx = binary_and((cx + dx), 0xFFFFFFFF)
    | extend bx = binary_and((binary_xor(bx, cx)), 0xFFFFFFFF)
    | extend bx = binary_and((CHACHA20_ROTL(bx, 7)), 0xFFFFFFFF)
    | project a = ax, b = bx, c = cx, d = dx
};
let c0 = datatable(id:int) [0, 4, 8, 12];
let r1c0t = c0 | join kind=inner ChaCha20_init on $left.id == $right.id | project a = iff(id == 0, state, 0), b = iff(id == 4, state, 0), c = iff(id == 8, state, 0), d = iff(id == 12, state, 0) | summarize a=toint(take_anyif(a, a != 0)), b=toint(take_anyif(b, b != 0)), c=toint(take_anyif(c, c != 0)), d=toint(take_anyif(d, d != 0));
let r1c0 = materialize(union withsource=Row CHACHA20_QR(r1c0t) | project a, b, c, d, id = 10);
let c1 = datatable(id:int) [1, 5, 9, 13];
let r1c1t = c1 | join kind=inner ChaCha20_init on $left.id == $right.id | project a = iff(id == 1, state, 0), b = iff(id == 5, state, 0), c = iff(id == 9, state, 0), d = iff(id == 13, state, 0) | summarize a=toint(take_anyif(a, a != 0)), b=toint(take_anyif(b, b != 0)), c=toint(take_anyif(c, c != 0)), d=toint(take_anyif(d, d != 0));
let r1c1 = materialize(union withsource=Row CHACHA20_QR(r1c1t) | project a, b, c, d, id = 11);
let c2 = datatable(id:int) [2, 6, 10, 14];
let r1c2t = c2 | join kind=inner ChaCha20_init on $left.id == $right.id | project a = iff(id == 2, state, 0), b = iff(id == 6, state, 0), c = iff(id == 10, state, 0), d = iff(id == 14, state, 0) | summarize a=toint(take_anyif(a, a != 0)), b=toint(take_anyif(b, b != 0)), c=toint(take_anyif(c, c != 0)), d=toint(take_anyif(d, d != 0));
let r1c2 = materialize(union withsource=Row CHACHA20_QR(r1c2t) | project a, b, c, d, id = 12);
let c3 = datatable(id:int) [3, 7, 11, 15];
let r1c3t = c3 | join kind=inner ChaCha20_init on $left.id == $right.id | project a = iff(id == 3, state, 0), b = iff(id == 7, state, 0), c = iff(id == 11, state, 0), d = iff(id == 15, state, 0) | summarize a=toint(take_anyif(a, a != 0)), b=toint(take_anyif(b, b != 0)), c=toint(take_anyif(c, c != 0)), d=toint(take_anyif(d, d != 0));
let r1c3 = materialize(union withsource=Row CHACHA20_QR(r1c3t) | project a, b, c, d, id = 13);
let r1d1t = union r1c0, r1c1, r1c2, r1c3 | project a = iff(id == 10, a, 0), b = iff(id == 11, b, 0), c = iff(id == 12, c, 0), d = iff(id == 13, d, 0) | summarize a=toint(take_anyif(a, a != 0)), b=toint(take_anyif(b, b != 0)), c=toint(take_anyif(c, c != 0)), d=toint(take_anyif(d, d != 0));
let r1d1 = materialize(union withsource=Row CHACHA20_QR(r1d1t) | project a, b, c, d, id = 10);
let r1d2t = union r1c0, r1c1, r1c2, r1c3 | project a = iff(id == 11, a, 0), b = iff(id == 12, b, 0), c = iff(id == 13, c, 0), d = iff(id == 10, d, 0) | summarize a=toint(take_anyif(a, a != 0)), b=toint(take_anyif(b, b != 0)), c=toint(take_anyif(c, c != 0)), d=toint(take_anyif(d, d != 0));
let r1d2 = materialize(union withsource=Row CHACHA20_QR(r1d2t) | project a, b, c, d, id = 11);
let r1d3t = union r1c0, r1c1, r1c2, r1c3 | project a = iff(id == 12, a, 0), b = iff(id == 13, b, 0), c = iff(id == 10, c, 0), d = iff(id == 11, d, 0) | summarize a=toint(take_anyif(a, a != 0)), b=toint(take_anyif(b, b != 0)), c=toint(take_anyif(c, c != 0)), d=toint(take_anyif(d, d != 0));
let r1d3 = materialize(union withsource=Row CHACHA20_QR(r1d3t) | project a, b, c, d, id = 12);
let r1d4t = union r1c0, r1c1, r1c2, r1c3 | project a = iff(id == 13, a, 0), b = iff(id == 10, b, 0), c = iff(id == 11, c, 0), d = iff(id == 12, d, 0) | summarize a=toint(take_anyif(a, a != 0)), b=toint(take_anyif(b, b != 0)), c=toint(take_anyif(c, c != 0)), d=toint(take_anyif(d, d != 0));
let r1d4 = materialize(union withsource=Row CHACHA20_QR(r1d4t) | project a, b, c, d, id = 13);
let r2c0t = union r1d1, r1d2, r1d3, r1d4 | project a = iff(id == 10, a, 0), b = iff(id == 13, b, 0), c = iff(id == 12, c, 0), d = iff(id == 11, d, 0) | summarize a=toint(take_anyif(a, a != 0)), b=toint(take_anyif(b, b != 0)), c=toint(take_anyif(c, c != 0)), d=toint(take_anyif(d, d != 0));
let r2c0 = materialize(union withsource=Row CHACHA20_QR(r2c0t) | project a, b, c, d, id = 10);
let r2c1t = union r1d1, r1d2, r1d3, r1d4 | project a = iff(id == 11, a, 0), b = iff(id == 10, b, 0), c = iff(id == 13, c, 0), d = iff(id == 12, d, 0) | summarize a=toint(take_anyif(a, a != 0)), b=toint(take_anyif(b, b != 0)), c=toint(take_anyif(c, c != 0)), d=toint(take_anyif(d, d != 0));
let r2c1 = materialize(union withsource=Row CHACHA20_QR(r2c1t) | project a, b, c, d, id = 11);
let r2c2t = union r1d1, r1d2, r1d3, r1d4 | project a = iff(id == 12, a, 0), b = iff(id == 11, b, 0), c = iff(id == 10, c, 0), d = iff(id == 13, d, 0) | summarize a=toint(take_anyif(a, a != 0)), b=toint(take_anyif(b, b != 0)), c=toint(take_anyif(c, c != 0)), d=toint(take_anyif(d, d != 0));
let r2c2 = materialize(union withsource=Row CHACHA20_QR(r2c2t) | project a, b, c, d, id = 12);
let r2c3t = union r1d1, r1d2, r1d3, r1d4 | project a = iff(id == 13, a, 0), b = iff(id == 12, b, 0), c = iff(id == 11, c, 0), d = iff(id == 10, d, 0) | summarize a=toint(take_anyif(a, a != 0)), b=toint(take_anyif(b, b != 0)), c=toint(take_anyif(c, c != 0)), d=toint(take_anyif(d, d != 0));
let r2c3 = materialize(union withsource=Row CHACHA20_QR(r2c3t) | project a, b, c, d, id = 13);
let r2d1t = union r2c0, r2c1, r2c2, r2c3 | project a = iff(id == 10, a, 0), b = iff(id == 11, b, 0), c = iff(id == 12, c, 0), d = iff(id == 13, d, 0) | summarize a=toint(take_anyif(a, a != 0)), b=toint(take_anyif(b, b != 0)), c=toint(take_anyif(c, c != 0)), d=toint(take_anyif(d, d != 0));
let r2d1 = materialize(union withsource=Row CHACHA20_QR(r2d1t) | project a, b, c, d, id = 10);
let r2d2t = union r2c0, r2c1, r2c2, r2c3 | project a = iff(id == 11, a, 0), b = iff(id == 12, b, 0), c = iff(id == 13, c, 0), d = iff(id == 10, d, 0) | summarize a=toint(take_anyif(a, a != 0)), b=toint(take_anyif(b, b != 0)), c=toint(take_anyif(c, c != 0)), d=toint(take_anyif(d, d != 0));
let r2d2 = materialize(union withsource=Row CHACHA20_QR(r2d2t) | project a, b, c, d, id = 11);
let r2d3t = union r2c0, r2c1, r2c2, r2c3 | project a = iff(id == 12, a, 0), b = iff(id == 13, b, 0), c = iff(id == 10, c, 0), d = iff(id == 11, d, 0) | summarize a=toint(take_anyif(a, a != 0)), b=toint(take_anyif(b, b != 0)), c=toint(take_anyif(c, c != 0)), d=toint(take_anyif(d, d != 0));
let r2d3 = materialize(union withsource=Row CHACHA20_QR(r2d3t) | project a, b, c, d, id = 12);
let r2d4t = union r2c0, r2c1, r2c2, r2c3 | project a = iff(id == 13, a, 0), b = iff(id == 10, b, 0), c = iff(id == 11, c, 0), d = iff(id == 12, d, 0) | summarize a=toint(take_anyif(a, a != 0)), b=toint(take_anyif(b, b != 0)), c=toint(take_anyif(c, c != 0)), d=toint(take_anyif(d, d != 0));
let r2d4 = materialize(union withsource=Row CHACHA20_QR(r2d4t) | project a, b, c, d, id = 13);
let keyStream =
    union r2d1, r2d2, r2d3, r2d4
    | extend o0 = iff(id == 10, a, 0), o5 = iff(id == 10, b, 0), o10 = iff(id == 10, c, 0), o15 = iff(id == 10, d, 0)
    | extend o1 = iff(id == 11, a, 0), o6 = iff(id == 11, b, 0), o11 = iff(id == 11, c, 0), o12 = iff(id == 11, d, 0)
    | extend o2 = iff(id == 12, a, 0), o7 = iff(id == 12, b, 0), o8 = iff(id == 12, c, 0), o13 = iff(id == 12, d, 0)
    | extend o3 = iff(id == 13, a, 0), o4 = iff(id == 13, b, 0), o9 = iff(id == 13, c, 0), o14 = iff(id == 13, d, 0)
    | summarize o0=take_anyif(o0, o0 != 0), o1=take_anyif(o1, o1 != 0), o2=take_anyif(o2, o2 != 0), o3=take_anyif(o3, o3 != 0),
            o4=take_anyif(o4, o4 != 0), o5=take_anyif(o5, o5 != 0), o6=take_anyif(o6, o6 != 0), o7=take_anyif(o7, o7 != 0),
            o8=take_anyif(o8, o8 != 0), o9=take_anyif(o9, o9 != 0), o10=take_anyif(o10, o10 != 0), o11=take_anyif(o11, o11 != 0),
            o12=take_anyif(o12, o12 != 0), o13=take_anyif(o13, o13 != 0), o14=take_anyif(o14, o14 != 0), o15=take_anyif(o15, o15 != 0)
    | project state_array = pack_array(o0, o1, o2, o3, o4, o5, o6, o7, o8, o9, o10, o11, o12, o13, o14, o15)
    | mv-expand with_itemindex=i CipherBlock=state_array to typeof(long)
    | join kind=inner ChaCha20_init on $left.i == $right.id
    | extend CipherBlock = binary_and(CipherBlock + state, 0xFFFFFFFF)
    | extend Byte0 = binary_and(binary_shift_right(CipherBlock, 3*8), 0xFF), Byte1 = binary_and(binary_shift_right(CipherBlock, 2*8), 0xFF), Byte2 = binary_and(binary_shift_right(CipherBlock, 1*8), 0xFF), Byte3 = binary_and(binary_shift_right(CipherBlock, 0*8), 0xFF)
    | project keyByte = pack_array(Byte3, Byte2, Byte1, Byte0)
    | mv-expand keyByte
    | summarize keyByte = make_list(keyByte)
    | mv-expand with_itemindex=byteIndex keyByte to typeof(long);
let dataStream = 
    print data
    | mv-expand with_itemindex=byteIndex dataByte=data to typeof(long)
    | where byteIndex <= 64
    | project dataByte, byteIndex;
let outputData = 
    dataStream
    | join kind=inner keyStream on byteIndex
    | extend CipherData = binary_and(binary_xor(dataByte, keyByte), 0xFFFFFFFF)
    | summarize array = make_list(CipherData)
    | extend message = unicode_codepoints_to_string(array), Title = "Output";
let inputData = 
    print array=data
    | extend message = unicode_codepoints_to_string(array), Title = "Input";
union outputData, inputData
| project-reorder Title, message, array






!==!==!==!==!==!==!==!==!==!==!==!==!==!==!==!==!==!==!==!==!==!==!==!==!==!==!==!==!==!==!==!==!==!==!==!==!==!==!==!==






// * Version with comments *

// Inspiration for the cryptographic logic (C implementation): https://github.com/marcizhu/ChaCha20
//      Implementation can be verified using: https://onlinegdb.com/7rH7Jssv3
//
// Input data
let data = dynamic([112, 190, 20, 202, 133, 230, 89, 84, 119, 96, 250, 70, 48, 134, 114, 95, 42, 68, 174, 84, 21, 101, 145, 85, 108]);
let keySize = 32;
let nonceSize = 12;
let count = 0x1;
let key = dynamic([0xb0, 0x92, 0x57, 0x0c, 0xf2, 0x38, 0xb4, 0x1c, 0x67, 0xa0, 0xc3, 0x71, 0x16, 0x51, 0x07, 0x59, 0x12, 0x94, 0xe0, 0x80, 0x54, 0x13, 0x37, 0x2e, 0x16, 0x2b, 0xe9, 0x04, 0xa2, 0x51, 0xe2, 0xc6]);
let nonce = dynamic([0x10, 0xBA, 0x5E, 0x13, 0x37, 0xBA, 0x5E, 0x01, 0x33, 0x41, 0x85, 0x9f]);
let CHACHA20_CONSTANT = toscalar(unicode_codepoints_from_string("expand 32-byte k"));
// General functions
//
// Combine four 32-bit integers into one 32-bit
//
let pack4 = (a:dynamic)
{
    toint(binary_or(binary_shift_left(toint(a[0]), (0 * 8)), binary_or(binary_shift_left(toint(a[1]), (1 * 8)), binary_or(binary_shift_left(toint(a[2]), (2 * 8)), binary_shift_left(toint(a[3]), (3 * 8))))))
};
// *-* ChaCha20 cryptography functions
//
// Initialize the initial key stream based on the ChaCha20 constant, the given key and the given nonce
//
let ChaCha20_init = materialize(
    print x=0
    | extend state00 = toint(toscalar(pack4(array_slice(CHACHA20_CONSTANT, 0 * 4, array_length(CHACHA20_CONSTANT)))))
    | extend state01 = toint(toscalar(pack4(array_slice(CHACHA20_CONSTANT, 1 * 4, array_length(CHACHA20_CONSTANT)))))
    | extend state02 = toint(toscalar(pack4(array_slice(CHACHA20_CONSTANT, 2 * 4, array_length(CHACHA20_CONSTANT)))))
    | extend state03 = toint(toscalar(pack4(array_slice(CHACHA20_CONSTANT, 3 * 4, array_length(CHACHA20_CONSTANT)))))
    | extend state04 = toint(toscalar(pack4(array_slice(key, 0 * 4, array_length(key)))))
    | extend state05 = toint(toscalar(pack4(array_slice(key, 1 * 4, array_length(key)))))
    | extend state06 = toint(toscalar(pack4(array_slice(key, 2 * 4, array_length(key)))))
    | extend state07 = toint(toscalar(pack4(array_slice(key, 3 * 4, array_length(key)))))
    | extend state08 = toint(toscalar(pack4(array_slice(key, 4 * 4, array_length(key)))))
    | extend state09 = toint(toscalar(pack4(array_slice(key, 5 * 4, array_length(key)))))
    | extend state10 = toint(toscalar(pack4(array_slice(key, 6 * 4, array_length(key)))))
    | extend state11 = toint(toscalar(pack4(array_slice(key, 7 * 4, array_length(key)))))
    | extend state12 = toint(count)
    | extend state13 = toint(toscalar(pack4(array_slice(nonce, 0 * 4, array_length(key)))))
    | extend state14 = toint(toscalar(pack4(array_slice(nonce, 1 * 4, array_length(key)))))
    | extend state15 = toint(toscalar(pack4(array_slice(nonce, 2 * 4, array_length(key)))))
    | project-away x
    | project state = pack_array(*)
    | mv-expand with_itemindex=id state);
//
//  Computes the result of bitwise left-rotating the value x by n positions
//
let CHACHA20_ROTL = (x:long, n:long) 
{ 
    binary_or(binary_shift_left(x,  n), binary_shift_right(x, (32 - n)))
};
//
// Perform a quarter-round on the given values
//
let CHACHA20_QR = (T: (a:int, b:int, c:int, d:int)) 
{
    T
    | extend ax = a, bx = b, cx = c, dx = d
    // Have to handle the numbers as signed 64-bit for calculation, as they are in theory unsigned 32-bit which Kusto does not support yet
    | extend ax = binary_and(tolong(ax + bx), 0xFFFFFFFF)
    | extend dx = binary_and((binary_xor(dx, ax)), 0xFFFFFFFF)
    | extend dx = binary_and((CHACHA20_ROTL(dx, 16)), 0xFFFFFFFF)
    | extend cx = binary_and((cx + dx), 0xFFFFFFFF)
    | extend bx = binary_and((binary_xor(bx, cx)), 0xFFFFFFFF)
    | extend bx = binary_and((CHACHA20_ROTL(bx, 12)), 0xFFFFFFFF)
    | extend ax = binary_and((ax + bx), 0xFFFFFFFF)
    | extend dx = binary_and(((binary_xor(dx, ax))), 0xFFFFFFFF)
    | extend dx = binary_and(((CHACHA20_ROTL(dx, 8))), 0xFFFFFFFF)
    | extend cx = binary_and((cx + dx), 0xFFFFFFFF)
    | extend bx = binary_and((binary_xor(bx, cx)), 0xFFFFFFFF)
    | extend bx = binary_and((CHACHA20_ROTL(bx, 7)), 0xFFFFFFFF)
    | project a = ax, b = bx, c = cx, d = dx
};
//
// Perform the rounds on the key to achieve ChaCha4 (2 rounds, column & diagonal)
// Doing more rounds will crash the query, as the remote connection is forcibly closed (probably due to memory consumption)
//
// Round 1/2 - Column 0 - 3
let c0 = datatable(id:int) [0, 4, 8, 12];
let r1c0t = c0 | join kind=inner ChaCha20_init on $left.id == $right.id | project a = iff(id == 0, state, 0), b = iff(id == 4, state, 0), c = iff(id == 8, state, 0), d = iff(id == 12, state, 0) | summarize a=toint(take_anyif(a, a != 0)), b=toint(take_anyif(b, b != 0)), c=toint(take_anyif(c, c != 0)), d=toint(take_anyif(d, d != 0));
let r1c0 = materialize(union withsource=Row CHACHA20_QR(r1c0t) | project a, b, c, d, id = 10);
let c1 = datatable(id:int) [1, 5, 9, 13];
let r1c1t = c1 | join kind=inner ChaCha20_init on $left.id == $right.id | project a = iff(id == 1, state, 0), b = iff(id == 5, state, 0), c = iff(id == 9, state, 0), d = iff(id == 13, state, 0) | summarize a=toint(take_anyif(a, a != 0)), b=toint(take_anyif(b, b != 0)), c=toint(take_anyif(c, c != 0)), d=toint(take_anyif(d, d != 0));
let r1c1 = materialize(union withsource=Row CHACHA20_QR(r1c1t) | project a, b, c, d, id = 11);
let c2 = datatable(id:int) [2, 6, 10, 14];
let r1c2t = c2 | join kind=inner ChaCha20_init on $left.id == $right.id | project a = iff(id == 2, state, 0), b = iff(id == 6, state, 0), c = iff(id == 10, state, 0), d = iff(id == 14, state, 0) | summarize a=toint(take_anyif(a, a != 0)), b=toint(take_anyif(b, b != 0)), c=toint(take_anyif(c, c != 0)), d=toint(take_anyif(d, d != 0));
let r1c2 = materialize(union withsource=Row CHACHA20_QR(r1c2t) | project a, b, c, d, id = 12);
let c3 = datatable(id:int) [3, 7, 11, 15];
let r1c3t = c3 | join kind=inner ChaCha20_init on $left.id == $right.id | project a = iff(id == 3, state, 0), b = iff(id == 7, state, 0), c = iff(id == 11, state, 0), d = iff(id == 15, state, 0) | summarize a=toint(take_anyif(a, a != 0)), b=toint(take_anyif(b, b != 0)), c=toint(take_anyif(c, c != 0)), d=toint(take_anyif(d, d != 0));
let r1c3 = materialize(union withsource=Row CHACHA20_QR(r1c3t) | project a, b, c, d, id = 13);
// Round 1/2 - Diagonal 1 - 4
let r1d1t = union r1c0, r1c1, r1c2, r1c3 | project a = iff(id == 10, a, 0), b = iff(id == 11, b, 0), c = iff(id == 12, c, 0), d = iff(id == 13, d, 0) | summarize a=toint(take_anyif(a, a != 0)), b=toint(take_anyif(b, b != 0)), c=toint(take_anyif(c, c != 0)), d=toint(take_anyif(d, d != 0));
let r1d1 = materialize(union withsource=Row CHACHA20_QR(r1d1t) | project a, b, c, d, id = 10);
let r1d2t = union r1c0, r1c1, r1c2, r1c3 | project a = iff(id == 11, a, 0), b = iff(id == 12, b, 0), c = iff(id == 13, c, 0), d = iff(id == 10, d, 0) | summarize a=toint(take_anyif(a, a != 0)), b=toint(take_anyif(b, b != 0)), c=toint(take_anyif(c, c != 0)), d=toint(take_anyif(d, d != 0));
let r1d2 = materialize(union withsource=Row CHACHA20_QR(r1d2t) | project a, b, c, d, id = 11);
let r1d3t = union r1c0, r1c1, r1c2, r1c3 | project a = iff(id == 12, a, 0), b = iff(id == 13, b, 0), c = iff(id == 10, c, 0), d = iff(id == 11, d, 0) | summarize a=toint(take_anyif(a, a != 0)), b=toint(take_anyif(b, b != 0)), c=toint(take_anyif(c, c != 0)), d=toint(take_anyif(d, d != 0));
let r1d3 = materialize(union withsource=Row CHACHA20_QR(r1d3t) | project a, b, c, d, id = 12);
let r1d4t = union r1c0, r1c1, r1c2, r1c3 | project a = iff(id == 13, a, 0), b = iff(id == 10, b, 0), c = iff(id == 11, c, 0), d = iff(id == 12, d, 0) | summarize a=toint(take_anyif(a, a != 0)), b=toint(take_anyif(b, b != 0)), c=toint(take_anyif(c, c != 0)), d=toint(take_anyif(d, d != 0));
let r1d4 = materialize(union withsource=Row CHACHA20_QR(r1d4t) | project a, b, c, d, id = 13);
// Round 2/2 - Column 0 - 3
let r2c0t = union r1d1, r1d2, r1d3, r1d4 | project a = iff(id == 10, a, 0), b = iff(id == 13, b, 0), c = iff(id == 12, c, 0), d = iff(id == 11, d, 0) | summarize a=toint(take_anyif(a, a != 0)), b=toint(take_anyif(b, b != 0)), c=toint(take_anyif(c, c != 0)), d=toint(take_anyif(d, d != 0));
let r2c0 = materialize(union withsource=Row CHACHA20_QR(r2c0t) | project a, b, c, d, id = 10);
let r2c1t = union r1d1, r1d2, r1d3, r1d4 | project a = iff(id == 11, a, 0), b = iff(id == 10, b, 0), c = iff(id == 13, c, 0), d = iff(id == 12, d, 0) | summarize a=toint(take_anyif(a, a != 0)), b=toint(take_anyif(b, b != 0)), c=toint(take_anyif(c, c != 0)), d=toint(take_anyif(d, d != 0));
let r2c1 = materialize(union withsource=Row CHACHA20_QR(r2c1t) | project a, b, c, d, id = 11);
let r2c2t = union r1d1, r1d2, r1d3, r1d4 | project a = iff(id == 12, a, 0), b = iff(id == 11, b, 0), c = iff(id == 10, c, 0), d = iff(id == 13, d, 0) | summarize a=toint(take_anyif(a, a != 0)), b=toint(take_anyif(b, b != 0)), c=toint(take_anyif(c, c != 0)), d=toint(take_anyif(d, d != 0));
let r2c2 = materialize(union withsource=Row CHACHA20_QR(r2c2t) | project a, b, c, d, id = 12);
let r2c3t = union r1d1, r1d2, r1d3, r1d4 | project a = iff(id == 13, a, 0), b = iff(id == 12, b, 0), c = iff(id == 11, c, 0), d = iff(id == 10, d, 0) | summarize a=toint(take_anyif(a, a != 0)), b=toint(take_anyif(b, b != 0)), c=toint(take_anyif(c, c != 0)), d=toint(take_anyif(d, d != 0));
let r2c3 = materialize(union withsource=Row CHACHA20_QR(r2c3t) | project a, b, c, d, id = 13);
// Round 2/2 - Diagonal 1 - 4
let r2d1t = union r2c0, r2c1, r2c2, r2c3 | project a = iff(id == 10, a, 0), b = iff(id == 11, b, 0), c = iff(id == 12, c, 0), d = iff(id == 13, d, 0) | summarize a=toint(take_anyif(a, a != 0)), b=toint(take_anyif(b, b != 0)), c=toint(take_anyif(c, c != 0)), d=toint(take_anyif(d, d != 0));
let r2d1 = materialize(union withsource=Row CHACHA20_QR(r2d1t) | project a, b, c, d, id = 10);
let r2d2t = union r2c0, r2c1, r2c2, r2c3 | project a = iff(id == 11, a, 0), b = iff(id == 12, b, 0), c = iff(id == 13, c, 0), d = iff(id == 10, d, 0) | summarize a=toint(take_anyif(a, a != 0)), b=toint(take_anyif(b, b != 0)), c=toint(take_anyif(c, c != 0)), d=toint(take_anyif(d, d != 0));
let r2d2 = materialize(union withsource=Row CHACHA20_QR(r2d2t) | project a, b, c, d, id = 11);
let r2d3t = union r2c0, r2c1, r2c2, r2c3 | project a = iff(id == 12, a, 0), b = iff(id == 13, b, 0), c = iff(id == 10, c, 0), d = iff(id == 11, d, 0) | summarize a=toint(take_anyif(a, a != 0)), b=toint(take_anyif(b, b != 0)), c=toint(take_anyif(c, c != 0)), d=toint(take_anyif(d, d != 0));
let r2d3 = materialize(union withsource=Row CHACHA20_QR(r2d3t) | project a, b, c, d, id = 12);
let r2d4t = union r2c0, r2c1, r2c2, r2c3 | project a = iff(id == 13, a, 0), b = iff(id == 10, b, 0), c = iff(id == 11, c, 0), d = iff(id == 12, d, 0) | summarize a=toint(take_anyif(a, a != 0)), b=toint(take_anyif(b, b != 0)), c=toint(take_anyif(c, c != 0)), d=toint(take_anyif(d, d != 0));
let r2d4 = materialize(union withsource=Row CHACHA20_QR(r2d4t) | project a, b, c, d, id = 13);
// *-* Printing functions
//
// Calculate the key stream
//
let keyStream =
    // Get the latest interation of the cipher
    union r2d1, r2d2, r2d3, r2d4
    // Get the relevant position of the cipher quarterround
    | extend o0 = iff(id == 10, a, 0), o5 = iff(id == 10, b, 0), o10 = iff(id == 10, c, 0), o15 = iff(id == 10, d, 0)
    | extend o1 = iff(id == 11, a, 0), o6 = iff(id == 11, b, 0), o11 = iff(id == 11, c, 0), o12 = iff(id == 11, d, 0)
    | extend o2 = iff(id == 12, a, 0), o7 = iff(id == 12, b, 0), o8 = iff(id == 12, c, 0), o13 = iff(id == 12, d, 0)
    | extend o3 = iff(id == 13, a, 0), o4 = iff(id == 13, b, 0), o9 = iff(id == 13, c, 0), o14 = iff(id == 13, d, 0)
    // take_anyif() is quicker than sum() as it doesn't look at all rows
    | summarize o0=take_anyif(o0, o0 != 0), o1=take_anyif(o1, o1 != 0), o2=take_anyif(o2, o2 != 0), o3=take_anyif(o3, o3 != 0),
            o4=take_anyif(o4, o4 != 0), o5=take_anyif(o5, o5 != 0), o6=take_anyif(o6, o6 != 0), o7=take_anyif(o7, o7 != 0),
            o8=take_anyif(o8, o8 != 0), o9=take_anyif(o9, o9 != 0), o10=take_anyif(o10, o10 != 0), o11=take_anyif(o11, o11 != 0),
            o12=take_anyif(o12, o12 != 0), o13=take_anyif(o13, o13 != 0), o14=take_anyif(o14, o14 != 0), o15=take_anyif(o15, o15 != 0)
    // Create an array in the correct order and expand it with an index
    | project state_array = pack_array(o0, o1, o2, o3, o4, o5, o6, o7, o8, o9, o10, o11, o12, o13, o14, o15)
    | mv-expand with_itemindex=i CipherBlock=state_array to typeof(long)
    // Add the initial keystream on our output
    | join kind=inner ChaCha20_init on $left.i == $right.id
    | extend CipherBlock = binary_and(CipherBlock + state, 0xFFFFFFFF)
    // Extract the bytes from the 32-bit integer
    | extend Byte0 = binary_and(binary_shift_right(CipherBlock, 3*8), 0xFF), Byte1 = binary_and(binary_shift_right(CipherBlock, 2*8), 0xFF), Byte2 = binary_and(binary_shift_right(CipherBlock, 1*8), 0xFF), Byte3 = binary_and(binary_shift_right(CipherBlock, 0*8), 0xFF)
    // Re-order the 32-bit integer in the correct order
    | project keyByte = pack_array(Byte3, Byte2, Byte1, Byte0)
    // Expand it again and summarize it to get the index position in the full array
    | mv-expand keyByte
    | summarize keyByte = make_list(keyByte)
    // Expand the 32-bit integers to only have bytes in the correct order
    | mv-expand with_itemindex=byteIndex keyByte to typeof(long);
//
// Convert data stream into 32-bit blocks
//
let dataStream = 
    print data
    | mv-expand with_itemindex=byteIndex dataByte=data to typeof(long)
    // Only supporting 64-bytes max! Sorry!
    // Supporting more would require recalculating the key
    | where byteIndex <= 64
    | project dataByte, byteIndex;
//
// Calculate the output data by XORing the input with the calculated key stream 
//
let outputData = 
    dataStream
    | join kind=inner keyStream on byteIndex
    // XOR the byte
    | extend CipherData = binary_and(binary_xor(dataByte, keyByte), 0xFFFFFFFF)
    // Make an array so we can convert to Unicode
    | summarize array = make_list(CipherData)
    | extend message = unicode_codepoints_to_string(array), Title = "Output";
//
// Also convert input to Unicode to show the difference
//
let inputData = 
    print array=data
    | extend message = unicode_codepoints_to_string(array), Title = "Input";
//
// Print both data streams
//
union outputData, inputData
| project-reorder Title, message, array
```